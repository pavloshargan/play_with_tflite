apply plugin: 'com.android.application'

// Define your QNN SDK path (you can also make it a variable like qnnSdkVersion, etc.)
def qnnSdkDir = file("../qairt/2.31.0.250130")

// Copy QNN skel libs (v68, v69, v73, v75) into jniLibs/arm64-v8a.
task copyQnnLibs(type: Copy) {
    from(
            "${qnnSdkDir}/lib/hexagon-v68/unsigned",
            "${qnnSdkDir}/lib/hexagon-v69/unsigned",
            "${qnnSdkDir}/lib/hexagon-v73/unsigned",
            "${qnnSdkDir}/lib/hexagon-v75/unsigned"
    )
    // Place them in the official jniLibs folder so they end up in the APK
    into "${projectDir}/src/main/jniLibs/arm64-v8a"

    // Only copy Skel libs (for DSP)
    include("*Skel.so")
}

// Make sure this copy task runs before building the APK
preBuild.dependsOn(copyQnnLibs)


android {
    compileSdk 32
    buildToolsVersion '33.0.1'

    defaultConfig {
        applicationId "com.iwatake.viewandroidtflite"
        minSdkVersion 31
        targetSdkVersion 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_ARM_NEON=TRUE"
                abiFilters 'arm64-v8a'
            }
        }
//        ↑↑↑ 追加 ↑↑↑
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            packagingOptions {
                pickFirst "**/**.so"
            }
        }


    }

    externalNativeBuild {
        cmake {
            path "../../../play_with_tflite/ViewAndroid/app/src/main/cpp/CMakeLists.txt" // Ensure this path points to your CMakeLists.txt
            version "3.31.5" // Replace with your installed CMake version
        }
    }

    //    ↓↓↓ 追加 ↓↓↓
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    ndkVersion '25.1.8937393'
    namespace 'com.iwatake.viewandroidtflite'
    //    ↑↑↑ 追加 ↑↑↑
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation project(':opencv4_10')

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

//    ↓↓↓ 追加 ↓↓↓
    def camerax_version = "1.0.0-beta03"

// CameraX core library using camera2 implementation
    implementation "androidx.camera:camera-camera2:$camerax_version"
// CameraX Lifecycle Library
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
// CameraX View class
    implementation "androidx.camera:camera-view:1.0.0-alpha10"
//    ↑↑↑ 追加 ↑↑↑
}
